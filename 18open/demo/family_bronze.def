include: ../../usaco.def
page: family_bronze, basic_frame
    module: cpp
    namespace: std

    $(set:N=12)
    $my int N, char A[$(N)], char B[$(N)]
    &call open_In
        fscanf(In, "%d %s %s", &N, A, B)
        $dump N, A, B

        i_A = get_id(A)
        i_B = get_id(B)
        $dump i_A, i_B

        $for i=0:N
            $my char sA[$(N)], char sB[$(N)]
            fscanf(In, "%s %s", sA, sB)
            i_A = get_id(sA)
            i_B = get_id(sB)
            mothers[i_B] = i_A
            $print $i_B -> $i_A

    $my vector<int> AA
    $my vector<int> BB
    $(for: 0, 1 and AA, BB)
        j = $1
        $2.push_back(j)
        $while mothers[j]>=0
            j = mothers[j]
            $2.push_back(j)

    $my string rel
    $if AA.size()>BB.size()
        get_rel(rel, BB, AA, B, A)
    $else
        get_rel(rel, AA, BB, A, B)
    $(if:1)
        &call open_Out
            $print "%s", rel.c_str()


fncode: get_id(char* name): int
    $include <unordered_map>, <string>, <vector>
    $global unordered_map<string, int> name_set
    $global vector<int> mothers
    string s(name);
    $if name_set.count(s)==0
        name_set[s]=mothers.size()
        mothers.push_back(-1)
    return name_set[s]

fncode: get_rel(string& rel, vector<int>& AA, vector<int>& BB, char* A, char* B)
    n_A=AA.size()
    n_B=BB.size()
    n = n_B-n_A
    $dump n_B, n_A, n
    # n_A < n_B
    rel += A
    rel += " is the "
    $if n==0 && BB[1]==AA[1]
        rel = "SIBLINGS"
    $elif BB[n]==AA[0]
        $if n==1
            rel+= "mother of "
        $elif n==2
            rel+= "grand-mother of "
        $else
            $for i=0:n-2
                rel+= "great-"
            rel+= "grand-mother of "
        rel+= B
    $elif n_A>1 && BB[n+1]==AA[1]
        $if n==1
            rel+= "aunt of "
        $else
            $for i=0:n-1
                rel+= "great-"
            rel+= "aunt of "
        rel+= B
    $else
        rel="NOT RELATED"
        $for i=2:n_A
            $if BB[n+i]==AA[i]
                rel = "COUSINS"
                break

