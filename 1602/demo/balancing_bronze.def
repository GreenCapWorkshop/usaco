include: ../../usaco.def

page: balancing_bronze, basic_frame
    module: cpp

    $local N, B: int
    &call open_In
        $call scan_int, N, B

        $my int X[N], int Y[N]
        $for i=0:N
            $call scan_int, X[i], Y[i]
            # $dump i, X[i], Y[i]

    $my M=N
    $block
        # $call plan_A
        $call plan_B
    &call open_Out
        $print $M

    subcode: plan_A
        $for a=0:B:2
            $for b=0:B:2
                $my q1=0, q2=0, q3=0, q4=0
                $for i=0:N
                    $if X[i]<a 
                        $if Y[i]<b
                            q1 ++
                        $else
                            q2 ++
                    $else
                        $if Y[i]<b
                            q3 ++
                        $else
                            q4 ++
                m = q1
                $(for:2,3,4)
                    $if m<q$1
                        m=q$1
                $if M>m
                    M=m
                # $dump a,b,q1,q2,q3,q4,m,M

    subcode: plan_B
        $my int A[N], int B[N]
        $for i=0:N
            A[i]=X[i]+1
            B[i]=Y[i]+1
        std::sort(A, A+N)
        std::sort(B, B+N)

        $for ia=0:N-1
            $for ib=0:N-1
                $my q1=0, q2=0, q3=0, q4=0
                $for i=0:N
                    $if X[i]<A[ia] 
                        $if Y[i]<B[ib]
                            q1 ++
                        $else
                            q2 ++
                    $else
                        $if Y[i]<B[ib]
                            q3 ++
                        $else
                            q4 ++
                m = q1
                $(for:2,3,4)
                    $if m<q$1
                        m=q$1
                $if M>m
                    M=m
                    # $dump ia,ib,q1,q2,q3,q4,m,M
